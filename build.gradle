plugins {
    id 'java'
    id 'checkstyle'

    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'org.liquibase.gradle' version '2.1.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

    liquibaseRuntime 'org.postgresql:postgresql'
    liquibaseRuntime 'org.liquibase:liquibase-core:3.8.9'
    liquibaseRuntime 'org.springframework:spring-orm'
    liquibaseRuntime 'org.springframework.boot:spring-boot'
    liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-jpa'

    liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:4.5.0'

    liquibaseRuntime sourceSets.main.output

    testImplementation 'org.testcontainers:postgresql:1.16.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = '7.3.1'
}

checkstyle {
    configFile = rootProject.file('tools/checkstyle/checkstyle.xml')
    maxErrors(0)
}

liquibase {
    activities {
        main {
            driver 'org.postgresql.Driver'
            url 'jdbc:postgresql://localhost:5434/testDb'
            username 'test'
            password 'test'
            changeLogFile 'src/main/resources/db/changelog/changes/XXXX-changelog.yaml'

            referenceUrl 'hibernate:spring:com.example.demo.model?' +
                    'dialect=org.hibernate.dialect.PostgreSQL10Dialect&' +
                    'hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&' +
                    'hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy'
            logLevel 'debug'
        }
    }
}

springBoot {
    buildInfo {
        properties {
            time = null
        }
    }
}

compileJava.dependsOn processResources
processResources.dependsOn bootBuildInfo

diff.dependsOn compileJava
diffChangeLog.dependsOn compileJava
